plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

group 'eu.koboo.en2do'
version '3.1.5'

repositories {
    mavenCentral()
}

dependencies {
    // The driver itself
    api("org.mongodb:mongodb-driver-sync:$mongoDriverVersion")
    testImplementation("org.mongodb:mongodb-driver-sync:$mongoDriverVersion")

    // Eliminate boilerplate code
    compileOnly "org.projectlombok:lombok:$lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$lombokVersion"

    // Test-Suite runtime
    testImplementation "org.junit.jupiter:junit-jupiter-engine:$jupiterVersion"

    // SLF4j logger for test-suite
    testImplementation "org.slf4j:slf4j-jdk14:$slf4jVersion"
}

test {
    useJUnitPlatform()
}

compileJava {
    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11
    options.fork = true
    options.encoding = 'UTF-8'
    options.release.set(11)
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allJava
    archiveClassifier.set('sources')
}
task javadocJar(type: Jar) {
    from javadoc
    archiveClassifier.set('javadoc')
}

publishing {
    repositories {
        maven {
            url('https://reposilite.koboo.eu/releases')
            credentials {
                username(System.getenv('REPO_USER'))
                password(System.getenv('REPO_TOKEN'))
            }
        }
    }
    publications {
        maven(MavenPublication) {
            from components.java
            artifact sourcesJar
            artifact javadocJar
        }
    }
}

project.tasks.shadowJar.finalizedBy(project.tasks.javadocJar)
project.tasks.shadowJar.finalizedBy(project.tasks.sourcesJar)
project.tasks.publish.dependsOn(project.tasks.shadowJar)
